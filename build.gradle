buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'antlr'
    id 'maven-publish'
    id 'signing'
    id 'net.researchgate.release' version '3.1.0'
}

jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    excludes: [
                            '**/omql/parser/**',
                            '**/Immutable*Condition.class',
                            '**/CompileDebugger.class'
                    ])
        }))
    }
}

group 'com.github.loyalty'
version '0.0.1'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.immutables:value:2.11.1'
    annotationProcessor 'org.immutables:value:2.11.1'

    implementation 'net.jcip:jcip-annotations:1.0'
    implementation 'org.apache.commons:commons-lang3:3.18.0'
    implementation 'org.ow2.asm:asm:9.8'
    implementation 'org.ow2.asm:asm-commons:9.8'
    implementation 'org.ow2.asm:asm-util:9.8'
    implementation 'io.vavr:vavr:0.10.7'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.2'
    implementation 'org.antlr:antlr4-runtime:4.13.2'

    antlr 'org.antlr:antlr4:4.13.2'

    testCompileOnly 'org.immutables:value:2.11.1'
    testAnnotationProcessor 'org.immutables:value:2.11.1'

    testImplementation 'org.hamcrest:hamcrest:3.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.4'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.11.4'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'OMG'
                description = 'Object Matching on Getters - a Java library for pattern matching on object properties'
                url = 'https://github.com/JFernandez-am/omg'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'JFernandez'
                        name = 'Jose Fernandez'
                        email = 'JFernandez@loyalty.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/JFernandez-am/omg.git'
                    developerConnection = 'scm:git:ssh://github.com:JFernandez-am/omg.git'
                    url = 'https://github.com/JFernandez-am/omg'
                }
            }
        }
    }
}